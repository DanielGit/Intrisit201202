OUTPUT_ARCH(mips)
ENTRY(noahos_main)
MEMORY
{
    ram (wx) : ORIGIN = 0x400000, LENGTH = 64M
}

SECTIONS
{
	/**** Code and read-only data ****/
	.text :
	{
		_ftext = ABSOLUTE(.) ;	/* Start of code and read-only data	*/
		*(.text*)
		*(.gnu.linkonce.*)
		*(__libc*)
		_ecode = ABSOLUTE(.) ;	/* End of code				*/
		
		*(.rodata*)
		
		. = ALIGN(8);
		_etext = ABSOLUTE(.);	/* End of code and read-only data	*/
	} > ram
	
	/**** Initialised data ****/
	.data :
	{
		_fdata = ABSOLUTE(.);	/* Start of initialised data		*/
		*(.data*)
		*(.eh_frame*)
		*(.gcc_except_table*)
		
		. = ALIGN(8);
		__CTOR_LIST__ = ABSOLUTE(.);
		KEEP(*(SORT(.ctors*)))
		__CTOR_END__ = ABSOLUTE(.);
		__DTOR_LIST__ = ABSOLUTE(.);
		KEEP(*(SORT(.dtors*)))
		__DTOR_END__ = ABSOLUTE(.);
		
		_gp = ABSOLUTE(. + 0x7ff0); /* Base of small data			*/
		
		*(.lit8) 
		*(.lit4) 
		*(.sdata) 
		
		. = ALIGN(8);
		
		_edata  = ABSOLUTE(.);	/* End of initialised data		*/
	} > ram

	/**** Uninitialised data ****/
	_start_bss = .;			/* Start of uninitialised data		*/
	.sbss : 
	{ 
		*(.sbss) 
		*(.scommon)
	}
	.bss :
	{
		*(.bss)
		*(COMMON)
		. = ALIGN(8) ;
		_freemem = .;
	}
	_end_bss = . ;			/* End of unitialised data		*/

	.gptab.sdata : 
	{ 
		*(.gptab.data)
		*(.gptab.sdata) 
	}
	.gptab.sbss : 
	{ 
		*(.gptab.bss)
		*(.gptab.sbss)
	}
	
	/DISCARD/ :
	{
		*(.reginfo)
	}
	
	PROVIDE(etext = _etext);
	PROVIDE (edata = .);
	PROVIDE (end = .);
}


