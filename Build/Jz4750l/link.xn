OUTPUT_ARCH(mips)
ENTRY(BootEntry)
MEMORY
{
    ram (wx) : ORIGIN = 0x80000000, LENGTH = 64M
}

SECTIONS
{
	/**** Code and read-only data ****/
	.text :
	{
		_ftext = ABSOLUTE(.) ;	/* Start of code and read-only data	*/
		*(.text*)
		*(.gnu.linkonce.*)
		*(__libc*)
	    *(.rel.dyn)
		_ecode = ABSOLUTE(.) ;	/* End of code				*/
		
		*(.rodata*)
		
		. = ALIGN(8);
		_etext = ABSOLUTE(.);	/* End of code and read-only data	*/
	} > ram
	
	.mdata :
	{
		_start_mplayer_data = ABSOLUTE(.);
		../../UsrLib/libout/JZ4750L/mplayer.a:(.data)
		../../UsrLib/libout/JZ4750L/libvo.a:(.data)
		../../UsrLib/libout/JZ4750L/libao2.a:(.data)
		../../UsrLib/libout/JZ4750L/libinput.a:(.data)
		../../UsrLib/libout/JZ4750L/libmpcodecs.a:(.data)
		../../UsrLib/libout/JZ4750L/libaf.a:(.data)
		../../UsrLib/libout/JZ4750L/libmpdemux.a:(.data)
		../../UsrLib/libout/JZ4750L/stream.a:(.data)
		../../UsrLib/libout/JZ4750L/libswscale.a:(.data)
		../../UsrLib/libout/JZ4750L/libosd.a:(.data)
		../../UsrLib/libout/JZ4750L/libavformat.a:(.data)
		../../UsrLib/libout/JZ4750L/libavcodec.a:(.data)
		../../UsrLib/libout/JZ4750L/libavutil.a:(.data)
		../../UsrLib/libout/JZ4750L/libpostproc.a:(.data)
		../../UsrLib/libout/JZ4750L/liba52.a:(.data)
		../../UsrLib/libout/JZ4750L/libmpeg2.a:(.data)
		../../UsrLib/libout/JZ4750L/libfaad2.a:(.data)
		../../UsrLib/libout/JZ4750L/libvorbisidec.a:(.data)
		../../UsrLib/libout/JZ4750L/libosdep.a:(.data)
		../../UsrLib/libout/JZ4750L/libmad.a:(.data)
		../../UsrLib/libout/JZ4750L/spc.a:(.data)
		../../UsrLib/libout/JZ4750L/fipop.a:(.data)
		../../UsrLib/libout/JZ4750L/av_sync.a:(.data)
		_end_mplayer_data = ABSOLUTE(.);
	} > ram
	
	/**** Initialised data ****/
	.data :
	{
		_fdata = ABSOLUTE(.);	/* Start of initialised data		*/
		*(.data*)
		*(.eh_frame*)
		*(.gcc_except_table*)
		
		. = ALIGN(8);
		__START_ctros = ABSOLUTE(.);
		KEEP(*(SORT(.ctors*)))
		__END_dtros = ABSOLUTE(.);
		__START_dtros = ABSOLUTE(.);
		KEEP(*(SORT(.dtors*)))
		__END_dtros = ABSOLUTE(.);
		
		_gp = ABSOLUTE(. + 0x7ff0); /* Base of small data			*/
		
		*(.lit8) 
		*(.lit4) 
		*(.sdata) 
		
		. = ALIGN(8);
		
		_edata  = ABSOLUTE(.);	/* End of initialised data		*/
	} > ram

	.plugin :
	{
	    . = ALIGN(4096);
		*(.video_codec_sect*)
		. = ALIGN(4096);
		*(.audio_codec_sect*)		
		
	}

	/**** Uninitialised data ****/
	_start_bss = .;			/* Start of uninitialised data		*/

	.msbss :
	{
		_start_mplayer_sbss = ABSOLUTE(.);
		../../UsrLib/libout/JZ4750L/mplayer.a:(.sbss)
		../../UsrLib/libout/JZ4750L/libvo.a:(.sbss)
		../../UsrLib/libout/JZ4750L/libao2.a:(.sbss)
		../../UsrLib/libout/JZ4750L/libinput.a:(.sbss)
		../../UsrLib/libout/JZ4750L/libmpcodecs.a:(.sbss)
		../../UsrLib/libout/JZ4750L/libaf.a:(.sbss)
		../../UsrLib/libout/JZ4750L/libmpdemux.a:(.sbss)
		../../UsrLib/libout/JZ4750L/stream.a:(.sbss)
		../../UsrLib/libout/JZ4750L/libswscale.a:(.sbss)
		../../UsrLib/libout/JZ4750L/libosd.a:(.sbss)
		../../UsrLib/libout/JZ4750L/libavformat.a:(.sbss)
		../../UsrLib/libout/JZ4750L/libavcodec.a:(.sbss)
		../../UsrLib/libout/JZ4750L/libavutil.a:(.sbss)
		../../UsrLib/libout/JZ4750L/libpostproc.a:(.sbss)
		../../UsrLib/libout/JZ4750L/liba52.a:(.sbss)
		../../UsrLib/libout/JZ4750L/libmpeg2.a:(.sbss)
		../../UsrLib/libout/JZ4750L/libfaad2.a:(.sbss)
		../../UsrLib/libout/JZ4750L/libvorbisidec.a:(.sbss)
		../../UsrLib/libout/JZ4750L/libosdep.a:(.sbss)
		../../UsrLib/libout/JZ4750L/libmad.a:(.sbss)
		../../UsrLib/libout/JZ4750L/spc.a:(.sbss)
		../../UsrLib/libout/JZ4750L/fipop.a:(.sbss)
		../../UsrLib/libout/JZ4750L/av_sync.a:(.sbss)
		_end_mplayer_sbss = ABSOLUTE(.);
	}

	.sbss : 
	{ 
		*(.sbss) 
		*(.scommon)
	}

	.mbss :
	{
		_start_mplayer_bss = ABSOLUTE(.);
		../../UsrLib/libout/JZ4750L/mplayer.a:(.bss)
		../../UsrLib/libout/JZ4750L/libvo.a:(.bss)
		../../UsrLib/libout/JZ4750L/libao2.a:(.bss)
		../../UsrLib/libout/JZ4750L/libinput.a:(.bss)
		../../UsrLib/libout/JZ4750L/libmpcodecs.a:(.bss)
		../../UsrLib/libout/JZ4750L/libaf.a:(.bss)
		../../UsrLib/libout/JZ4750L/libmpdemux.a:(.bss)
		../../UsrLib/libout/JZ4750L/stream.a:(.bss)
		../../UsrLib/libout/JZ4750L/libswscale.a:(.bss)
		../../UsrLib/libout/JZ4750L/libosd.a:(.bss)
		../../UsrLib/libout/JZ4750L/libavformat.a:(.bss)
		../../UsrLib/libout/JZ4750L/libavcodec.a:(.bss)
		../../UsrLib/libout/JZ4750L/libavutil.a:(.bss)
		../../UsrLib/libout/JZ4750L/libpostproc.a:(.bss)
		../../UsrLib/libout/JZ4750L/liba52.a:(.bss)
		../../UsrLib/libout/JZ4750L/libmpeg2.a:(.bss)
		../../UsrLib/libout/JZ4750L/libfaad2.a:(.bss)
		../../UsrLib/libout/JZ4750L/libvorbisidec.a:(.bss)
		../../UsrLib/libout/JZ4750L/libosdep.a:(.bss)
		../../UsrLib/libout/JZ4750L/libmad.a:(.bss)
		../../UsrLib/libout/JZ4750L/spc.a:(.bss)
		../../UsrLib/libout/JZ4750L/fipop.a:(.bss)
		../../UsrLib/libout/JZ4750L/av_sync.a:(.bss)
		_end_mplayer_bss = ABSOLUTE(.);
	}
	
	.bss :
	{
		*(.bss)
		*(COMMON)
		. = ALIGN(8) ;
		_freemem = .;
	}
	_end_bss = . ;			/* End of unitialised data		*/

	.gptab.sdata : 
	{ 
		*(.gptab.data)
		*(.gptab.sdata) 
	}
	.gptab.sbss : 
	{ 
		*(.gptab.bss)
		*(.gptab.sbss)
	}
	
	/DISCARD/ :
	{
		*(.reginfo)
	}
	
	PROVIDE(etext = _etext);
	PROVIDE (edata = .);
	PROVIDE (end = .);
}


